/* test scanner code for dtds */
%{

#ifndef SOLARIS
#include <strings.h>
#endif
#include <string.h>
#include <errno.h>
#include "sgml.h"

#include "sgml_pars.h"

extern FILE *LOGFILE;

/* #define DEBUGPARSER 1 */ /* uncomment for tons of tracing info */

int parsed_file_chars = 0;

char *temp_parse_buffer = NULL;
int  temp_parse_buffer_size = TEMP_PARSE_BUFFER_SIZE;
int  temp_parse_buffer_differs = 0;

extern struct sgml_yylval { 
        int  tok_id;
        char *string;
        int intval;
	Tcl_HashEntry *hash_entry; 
 } sgml_yylval, sgml_yylval_pushback, sgml_yylval_include_back;

extern SGML_DTD *current_sgml_dtd; /* declared in sgml_gram.g */


#define YY_USER_ACTION 	sgml_realloc_buffer(parsed_file_chars, yytext); \
			parsed_file_chars += yyleng; \
		        sgml_yylval.string = NULL; \
			sgml_yylval.hash_entry = NULL;

int parsed_file_lineno = 1; 
char char_insert;
int  char_insert_size;

extern int sgml_parse_error;

#ifdef DMALLOC_INCLUDE
#include <dmalloc.h>
#endif

#ifdef DMALLOC_INCLUDE
#define YY_MALLOC_DECL
#endif


int sgml_prev_state[500];
int sgml_state = 0;
int sys_state; /* state when SYSTEM include call is made */
 
#define MAX_INCLUDE_DEPTH 20
YY_BUFFER_STATE include_stack[MAX_INCLUDE_DEPTH];
int include_stack_ptr = 0;
int include_entity = 0;
int string_entity = 0;
int templen;

char *backup_string;

extern int expect_otm; /* flag set in grammar file newgrammar.g */
extern int expect_document; /* flag set in grammar file newgrammar.g */
extern int default_entity;  /* set when #DEFAULT is used as entity name */
extern int expect_decl_value; /* set after the name in a attribute list */
extern char *up_case_string();

%}

/* Definitions */

DIGIT  [0-9]
DIGITS [0-9]+	
LETTER [a-zA-Z]

stago "<"
etago "</"
tagc  ">"
net "/"
mdo "<!"
mdc ">"
nullmdo "<!>"
comdcls "<!--"
comdcle "-->"
marked_sec_start "<!["
marked_sec_end   "]]>"
pio "<?"
pic ">"
xmlc "?>"
dso "["
dsc "]"
com "--"
cro "&#"
ws  [ \t]+
space " "
newline "\n"
vi "="


/* note that this doesn't allow names starting with . or - */
sgmlname [a-zA-Z][a-zA-Z0-9.-_:]*
name_token [a-zAZ0-9][a-zAZ0-9.-_:]*
param_lit ('[^']*')|(\"[^"]*\")
sysid_token ([a-zA-Z0-9.-_/~+]*)
entity_token ({sgmlname})|(%{sgmlname})


grpo "("
grpc ")"
rni  "#"
plus "+"
minus "-"
explus "+("
exminus "-("
equals "="
rep "*"
opt "?"
and "&"
or "|"
seq ","
dtgo "["
dtgc "]"

ero  "&"
ampersand_space  "& "
pero "%"
refc ";"

/* Start conditions */
%x sgml_comment
%x param_comment
%x sgml_decl
%x xml_decl
%x xml_version
%s sgml_doctype
%s sgml_attlist
%s sgml_element
%s sgml_entity
%s sgml_shortref
%s sgml_usemap
%s sgml_marked
%s sgml_notation
%x sgml_proc_inst
%x groups
%x mdo_start
%s newinclude
%x document
%x charref
%x docendtag
%x docstarttag
%x sgml_catalog

%%

<sgml_decl,sgml_doctype,sgml_attlist,sgml_element,sgml_entity,sgml_notation,groups,mdo_start,sgml_shortref,sgml_usemap,INITIAL,newinclude>{ws} {
         return TOKEN_SEP;
	}

<sgml_comment,param_comment>[^->\n]*  {return JUNK;} 
                                    /* get rid of anything but - and > */
<sgml_comment>{comdcle} {
		         if (sgml_state >= 1)
	     			BEGIN(sgml_prev_state[--sgml_state]); 	             
                         return COMDCLE;}

<param_comment>{com} 	{
		         if (sgml_state >= 1)
	     			BEGIN(sgml_prev_state[--sgml_state]); 	             
                         return COM;}

<sgml_comment,param_comment>"-"      {return JUNK;}
<sgml_comment,param_comment>">"      {return JUNK;}
<sgml_comment,param_comment>{newline} {
	parsed_file_lineno++;
	}



{comdcls} { 
         sgml_prev_state[sgml_state++] = YY_START;
         BEGIN(sgml_comment); 
         return COMDCLS;
}

<document>{comdcls} { 
         sgml_prev_state[sgml_state++] = YY_START;
         BEGIN(sgml_comment); 
         return COMDCLS;
}

<sgml_decl,mdo_start,sgml_attlist,sgml_element,sgml_entity,sgml_marked,sgml_shortref,sgml_usemap>{com} {
         sgml_prev_state[sgml_state++] = YY_START;
         BEGIN(param_comment); 
         return COM;
}


<sgml_attlist,sgml_element,sgml_entity,sgml_doctype,sgml_shortref,sgml_notation,sgml_usemap,INITIAL>{mdc} {
                          BEGIN(INITIAL); 
                          return MDC;}

{dsc}	 { return DSC;}

<sgml_doctype,sgml_attlist>{dso} {
	return DSO;
}

<sgml_entity,sgml_attlist,groups,sgml_usemap>{rni} { return RNI;}

<sgml_decl,sgml_doctype,sgml_attlist,sgml_element,sgml_entity,groups,mdo_start,sgml_shortref,sgml_usemap,INITIAL,newinclude>{pero}[a-zA-Z][a-zA-Z0-9.-]*[; )>\n]  {
	char *c;
	templen = yyleng - 2; 
	sgml_yylval.string = CALLOC(char, templen+1);
	strncpy(sgml_yylval.string, &yytext[1],templen);
	sgml_yylval.string[templen] = '\0';
	for (c = sgml_yylval.string; *c; c++) *c = toupper(*c);

	if (yytext[yyleng-1] == ')' || yytext[yyleng-1] == '>') {
	   yyless(yyleng-1);
	}

	return PERO_REF;
	}

{pero}	 { return PERO;}


<sgml_doctype,sgml_attlist,sgml_element,sgml_entity,groups,mdo_start,sgml_shortref,sgml_usemap,INITIAL>{grpo}	{
         sgml_prev_state[sgml_state++] = YY_START;
         BEGIN(groups);
         return GRPO;
	}

<groups>{grpc}	{
         if (sgml_state >= 1)
	     BEGIN(sgml_prev_state[--sgml_state]); 	             
	 return GRPC;
        }


<sgml_proc_inst>[^>]*  {return INSTRUCTION;} /* get rid of anything but > */
<sgml_proc_inst>{pic} 	{
		         if (sgml_state >= 1)
	     			BEGIN(sgml_prev_state[--sgml_state]); 	             
                         return PIC;}

{pio}   {
         sgml_prev_state[sgml_state++] = YY_START;
         BEGIN(sgml_proc_inst); 
         return PIO;
}

<xml_decl>("VERSION"|"version") {
				BEGIN(xml_version);
				return VERSIONINFO;}

<xml_version>{equals}  {return VI;}


<xml_version,xml_decl>{xmlc} { 
				BEGIN(INITIAL);
				return XML_ENDDECL;}



{pio}("XML"|"xml") {
				BEGIN(xml_decl);
				return XML_DECL;}


{mdo}("SGML"|"sgml")  {
				BEGIN(sgml_decl);
				return MDO_SGML;}

{mdo}("DOCTYPE"|"doctype")  {
				BEGIN(sgml_doctype);
				return MDO_DOCTYPE;}
{mdo}("ELEMENT"|"element")  {
				BEGIN(sgml_element); 
				return MDO_ELEMENT;}
{mdo}("ENTITY"|"entity")    {
				 BEGIN(sgml_entity);
				 return MDO_ENTITY;}

{mdo}("SHORTREF"|"shortref") {
			      BEGIN(sgml_shortref);
			      return MDO_SHORTREF;}

{mdo}("ATTLIST"|"attlist")  {
				BEGIN(sgml_attlist); 
				return MDO_ATTLIST;}
<sgml_marked>{marked_sec_end}  {
                          BEGIN(INITIAL); 
                          return MARKED_SECTION_END;}

{marked_sec_start}  { 
	        sgml_prev_state[sgml_state++] = YY_START;
		BEGIN(sgml_marked);
	        return MARKED_SECTION_START;
	}

<*>"CDATA" { return CDATA_DCL;}
<*>"SDATA" { return SDATA_DCL;}
<*>"NDATA" { return NDATA_DCL;}

<sgml_entity>"PI" { return PI_DCL;}
<sgml_entity>"STARTTAG" { return STARTTAG_DCL;}
<sgml_entity>"ENDTAG" { return ENDTAG_DCL;}
<sgml_entity>"MS" { return MS_DCL;}
<sgml_entity>"MD" { return MD_DCL;}
<sgml_entity>"DEFAULT" { return DEFAULT_DCL;}
<sgml_entity>"SUBDOC" { return SUBDOC_DCL;}

<sgml_marked>"TEMP" { return TEMP_DCL;}
<sgml_marked>"IGNORE" { return IGNORE_DCL;}
<sgml_marked>"INCLUDE" { return INCLUDE_DCL;}


<sgml_marked,sgml_entity,sgml_attlist,sgml_element>"RCDATA" {
                              return RCDATA_DCL;}
<sgml_entity,sgml_attlist,groups>"PCDATA" { return PCDATA_DCL;}
<sgml_usemap,sgml_element>"EMPTY" { return EMPTY_DCL;}
"ANY"   { return ANY;}


<sgml_attlist>"ENTITY" {if (expect_decl_value) return ENTITY_DCL;
			else {	sgml_yylval.string = up_case_string(strdup(yytext)); 
			        return SGMLNAME;
			}
		}
<sgml_attlist>"ENTITIES" {if (expect_decl_value) return ENTITIES_DCL;
			else {	sgml_yylval.string = up_case_string(strdup(yytext)); 
			        return SGMLNAME;
			}
		}
<sgml_attlist>"ID" {if (expect_decl_value) return ID_DCL;
			else {	sgml_yylval.string = up_case_string(strdup(yytext)); 
			        return SGMLNAME;
			}
		}
<sgml_attlist>"IDREF" {if (expect_decl_value) return IDREF_DCL;
			else {	sgml_yylval.string = up_case_string(strdup(yytext)); 
			        return SGMLNAME;
			}
		}
<sgml_attlist>"IDREFS" {if (expect_decl_value) return IDREFS_DCL;
			else {	sgml_yylval.string = up_case_string(strdup(yytext)); 
			        return SGMLNAME;
			}
		}
<sgml_attlist>"NAME" {if (expect_decl_value) return NAME_DCL;
			else {	sgml_yylval.string = up_case_string(strdup(yytext)); 
			        return SGMLNAME;
			}
		}
<sgml_attlist>"NAMES" {if (expect_decl_value) return NAMES_DCL;
			else {	sgml_yylval.string = up_case_string(strdup(yytext)); 
			        return SGMLNAME;
			}
		}
<sgml_attlist>"NMTOKEN" {if (expect_decl_value) return NMTOKEN_DCL;
			else {	sgml_yylval.string = up_case_string(strdup(yytext)); 
			        return SGMLNAME;
			}
		}
<sgml_attlist>"NMTOKENS" {if (expect_decl_value) return NMTOKENS_DCL;
			else {	sgml_yylval.string = up_case_string(strdup(yytext)); 
			        return SGMLNAME;
			}
		}
<sgml_attlist>"NUMBER" {if (expect_decl_value) return NUMBER_DCL;
			else {	sgml_yylval.string = up_case_string(strdup(yytext)); 
			        return SGMLNAME;
			}
		}
<sgml_attlist>"NUMBERS" {if (expect_decl_value) return NUMBERS_DCL;
			else {	sgml_yylval.string = up_case_string(strdup(yytext)); 
			        return SGMLNAME;
			}
		}
<sgml_attlist>"NUTOKEN" {if (expect_decl_value) return NUTOKEN_DCL;
			else {	sgml_yylval.string = up_case_string(strdup(yytext)); 
			        return SGMLNAME;
			}
		}
<sgml_attlist>"NUTOKENS" {if (expect_decl_value) return NUTOKENS_DCL;
			else {	sgml_yylval.string = up_case_string(strdup(yytext)); 
			        return SGMLNAME;
			}
		}


<sgml_attlist>"#FIXED"|"#fixed" {return RNI_FIXED_DCL;}
<sgml_attlist>"#CURRENT"|"#current" {return RNI_CURRENT_DCL;}
<sgml_attlist>"#CONREF"|"#conref" {return RNI_CONREF_DCL;}
<sgml_attlist>"#IMPLIED"|"#implied" {return RNI_IMPLIED_DCL;}
<sgml_attlist>"#REQUIRED"|"#required" {return RNI_REQUIRED_DCL;}
<sgml_attlist>"NOTATION"|"notation" {return NOTATION_DCL;}
<sgml_attlist>{equals}  {return VI;}


<sgml_decl>"CHARSET" {return CHARSET_DCL;}
<sgml_decl>"BASESET" {return BASESET_DCL;}
<sgml_decl>"DESCSET" {return DESCSET_DCL;}
<sgml_decl>"UNUSED" {return UNUSED_DCL;}
<sgml_decl>"CAPACITY" {return CAPACITY_DCL;}

<sgml_decl>"TOTALCAP" {return TOTALCAP_DCL;}
<sgml_decl>"ENTCAP" {return ENTCAP_DCL;}
<sgml_decl>"ENTCHCAP" {return ENTCHCAP_DCL;}
<sgml_decl>"ELEMCAP" {return ELEMCAP_DCL;}
<sgml_decl>"GRPCAP" {return GRPCAP_DCL;}
<sgml_decl>"EXGRPCAP" {return EXGRPCAP_DCL;}
<sgml_decl>"EXNMCAP" {return EXNMCAP_DCL;}
<sgml_decl>"ATTCAP" {return ATTCAP_DCL;}
<sgml_decl>"ATTCHCAP" {return ATTCHCAP_DCL;}
<sgml_decl>"AVGRPCAP" {return AVGRPCAP_DCL;}
<sgml_decl>"NOTCAP" {return NOTCAP_DCL;}
<sgml_decl>"NOTCHCAP" {return NOTCHCAP_DCL;}
<sgml_decl>"IDCAP" {return IDCAP_DCL;}
<sgml_decl>"IDREFCAP" {return IDREFCAP_DCL;}
<sgml_decl>"MAPCAP" {return MAPCAP_DCL;}
<sgml_decl>"LKSETCAP" {return LKSETCAP_DCL;}
<sgml_decl>"LKNMCAP" {return LKNMCAP_DCL;}

<sgml_decl>"SGMLREF" {return SGMLREF_DCL;}
<sgml_decl>"SHORTREF" {return SHORTREF_DCL;}
<sgml_decl>"SCOPE" {return SCOPE_DCL;}
<sgml_decl>"DOCUMENT" {return DOCUMENT_DCL;}
<sgml_decl>"INSTANCE" {return INSTANCE_DCL;}
<sgml_decl>"SYNTAX" {return SYNTAX_DCL;}
<sgml_decl>"SWITCHES" {return SWITCHES_DCL;}
<sgml_decl>"SHUNCHAR" {return SHUNCHAR_DCL;}
<sgml_decl>"CONTROLS" {return CONTROLS_DCL;}
<sgml_decl>"FUNCTION" {return FUNCTION_DCL;}
<sgml_decl>"RE" {return RE_DCL;}
<sgml_decl>"RS" {return RS_DCL;}
<sgml_decl>"SPACE" {return SPACE_DCL;}

<sgml_decl>"FUNCHAR" {return FUNCHAR_DCL;}
<sgml_decl>"MSICHAR" {return MSICHAR_DCL;}
<sgml_decl>"MSOCHAR" {return MSOCHAR_DCL;}
<sgml_decl>"MSSCHAR" {return MSSCHAR_DCL;}
<sgml_decl>"SEPCHAR" {return SEPCHAR_DCL;}
<sgml_decl>"NAMING" {return NAMING_DCL;}
<sgml_decl>"LCNMSTRT" {return LCNMSTRT_DCL;}
<sgml_decl>"UCNMSTRT" {return UCNMSTRT_DCL;}
<sgml_decl>"UCNMCHAR" {return UCNMCHAR_DCL;}
<sgml_decl>"LCNMCHAR" {return LCNMCHAR_DCL;}
<sgml_decl>"NAMECASE" {return NAMECASE_DCL;}
<sgml_decl>"GENERAL" {return GENERAL_DCL;}
<sgml_decl>"ENTITY" {return ENTITY_DCL;}
<sgml_decl>"NO" {return NO_DCL;}
<sgml_decl>"YES" {return YES_DCL;}
<sgml_decl>"NONE" {return NONE_DCL;}
<sgml_decl>"DELIM" {return DELIM_DCL;}
<sgml_decl>"NAMES" {return NAMES_DCL;}
<sgml_decl>"QUANTITY" {return QUANTITY_DCL;}

<sgml_decl>"ATTCNT" {return ATTCNT_DCL;}
<sgml_decl>"ATTSPLEN" {return ATTSPLEN_DCL;}
<sgml_decl>"BSEQLEN" {return BSEQLEN_DCL;}
<sgml_decl>"DTAGLEN" {return DTAGLEN_DCL;}
<sgml_decl>"DTEMPLEN" {return DTEMPLEN_DCL;}
<sgml_decl>"ENTLVL" {return ENTLVL_DCL;}
<sgml_decl>"GRPCNT" {return GRPCNT_DCL;}
<sgml_decl>"GRPGTCNT" {return GRPGTCNT_DCL;}
<sgml_decl>"GRPLVL" {return GRPLVL_DCL;}
<sgml_decl>"LITLEN" {return LITLEN_DCL;}
<sgml_decl>"NAMELEN" {return NAMELEN_DCL;}
<sgml_decl>"NORMSEP" {return NORMSEP_DCL;}
<sgml_decl>"PILEN" {return PILEN_DCL;}
<sgml_decl>"TAGLEN" {return TAGLEN_DCL;}
<sgml_decl>"TAGLVL" {return TAGLVL_DCL;}

<sgml_decl>"FEATURES" {return FEATURES_DCL;}
<sgml_decl>"MINIMIZE" {return MINIMIZE_DCL;}
<sgml_decl>"DATATAG" {return DATATAG_DCL;}
<sgml_decl>"OMITTAG" {return OMITTAG_DCL;}
<sgml_decl>"RANK" {return RANK_DCL;}
<sgml_decl>"SHORTTAG" {return SHORTTAG_DCL;}
<sgml_decl>"LINK" {return LINK_DCL;}
<sgml_decl>"SIMPLE" {return SIMPLE_DCL;}
<sgml_decl>"IMPLICIT" {return IMPLICIT_DCL;}
<sgml_decl>"EXPLICIT" {return EXPLICIT_DCL;}
<sgml_decl>"OTHER" {return OTHER_DCL;}
<sgml_decl>"CONCUR" {return CONCUR_DCL;}
<sgml_decl>"SUBDOC" {return SUBDOC_DCL;}
<sgml_decl>"FORMAL" {return FORMAL_DCL;}
<sgml_decl>"APPINFO" {return APPINFO_DCL;}

<sgml_decl>[0-9]+  {/* string not neededsgml_yylval.string = (yytext); */
	    sgml_yylval.string = NULL;
            sgml_yylval.intval = atoi(yytext);
            return NUMBER;
          }
<sgml_decl>{mdc} {BEGIN(INITIAL);
		  return MDC;
	 }


{mdo}"USEMAP" {BEGIN(sgml_usemap); return MDO_USEMAP_DCL;}
{mdo}"NOTATION" {BEGIN(sgml_notation); return MDO_NOTATION_DCL;}


<sgml_doctype>"SYSTEM" {
        
        sys_state = YY_START;
	return SYSTEM_DCL;
	}

<sgml_notation,sgml_entity>"SYSTEM" {
        
	return SYSTEM_DCL;
	}

<sgml_entity,sgml_notation,sgml_decl>"PUBLIC" {return PUBLIC_DCL;}


<sgml_element>[-oO]  {	sgml_yylval.string = up_case_string(strdup(yytext)); 
	     if (expect_otm)
		return OTM;
             else {
		
		return SGMLNAME;
	     }
	}


<sgml_decl,sgml_doctype,sgml_attlist,sgml_element,sgml_entity,sgml_notation,groups,mdo_start,sgml_shortref,sgml_usemap,INITIAL,newinclude>{sgmlname} { 
	sgml_yylval.string = up_case_string(strdup(yytext)); 
	if (expect_otm && (yytext[0] == 'o' || yytext[0] == 'O')) {
		return OTM;
	}
        else {
		return SGMLNAME;
	}
    }

<sgml_attlist,groups>{name_token}  { 
	char *c;
	sgml_yylval.string = up_case_string(strdup(yytext)); 
	for (c = sgml_yylval.string; *c ; c++) {
	    if (isalpha(*c)) 	return NAME_TOKEN;
	}
	/* if here there are no alpha chars */
	sgml_yylval.intval = atoi(sgml_yylval.string);
	return NUMBER;
	}

<groups>{and} {sgml_yylval.string = strdup(yytext); return AND;}
<groups>{or}  {sgml_yylval.string = strdup(yytext); return OR;}
<groups>{seq} {sgml_yylval.string = strdup(yytext); return SEQ;}
<groups>{dtgo} {return DTGO;}
<groups>{dtgc} {return DTGC;}
<groups,sgml_element>{rep}	{sgml_yylval.string = strdup(yytext);  return REP;}
<groups>{opt}	{sgml_yylval.string = strdup(yytext);  return OPT;}
<groups,sgml_element>{plus}  {sgml_yylval.string = strdup(yytext);  return PLUS;}
<sgml_element>{exminus}	{/* put back the open parenthesis */ 
		yyless(1);  
		return EXMINUS;
	}
<sgml_element>{explus}	{/* put back the open parenthesis */
		yyless(1);
		return EXPLUS;
	}

<sgml_decl,xml_decl,xml_version,groups,sgml_element,sgml_doctype,sgml_entity,sgml_notation,sgml_shortref>{param_lit}  {
	char *c; /* for line counting */
         
	templen = yyleng - 2; 
	sgml_yylval.string = CALLOC(char, templen+1);
	strncpy(sgml_yylval.string, &yytext[1],templen);
	sgml_yylval.string[templen] = '\0';
	/* keep the count of lines */
	for (c = sgml_yylval.string; *c ; c++) 
		if (*c == '\n') parsed_file_lineno++;
	return PARAMETER_LITERAL;
	}

<sgml_attlist,docstarttag>{param_lit}  {
	char *c;
       	templen = yyleng - 2; 
	sgml_yylval.string = CALLOC(char, templen+1);
	strncpy(sgml_yylval.string, &yytext[1],templen);
	sgml_yylval.string[templen] = '\0';
	/* keep the count of lines */
	for (c = sgml_yylval.string; *c ; c++) 
		if (*c == '\n') parsed_file_lineno++;

	return ATTR_VAL_LITERAL;
      }


<document>{stago} {
	BEGIN(docstarttag);
	return STAGO;
	}

<document>{etago} { 
	BEGIN(docendtag);
	return ETAGO;
	}

<docendtag,docstarttag>{tagc} { 
	BEGIN(document);
	return TAGC;
	}

<docendtag,docstarttag>{sgmlname} {
	sgml_yylval.string = up_case_string(strdup(yytext));
	sgml_yylval.intval = -1;

	return SGMLNAME;
	}


<docstarttag>{name_token}  { 
	char *c;
	sgml_yylval.string = up_case_string(strdup(yytext)); 
        
	for (c = sgml_yylval.string; *c ; c++) {
	    if (isalpha(*c)) {
		sgml_yylval.intval = -1;
		return NAME_TOKEN;
	    }
	}
	/* if here there are no alpha chars */
	sgml_yylval.intval = atoi(sgml_yylval.string);
	return NUMBER;
	}

<docendtag,docstarttag>{ws} {
	return TOKEN_SEP;
	}

<docendtag,docstarttag>{newline} {
	parsed_file_lineno++;
	return TOKEN_SEP;
	}

<docstarttag>{vi} {
	return VI;
	}

<document>{newline} {
	parsed_file_lineno++;
	sgml_yylval.string = strdup(yytext);
	return PCDATA_DATA;	
	}

<document>{ampersand_space} {
	sgml_yylval.string = strdup(yytext);
	return PCDATA_DATA;	
	}

<document>"#RESTORE"|"#restore" {return RNI_RESTORE_DCL;}
<document>"#INITIAL"|"#initial" {return RNI_INITIAL_DCL;}
<document>{cro} {BEGIN(charref); 
	char_insert = 0; 
	char_insert_size = yyleng;
	return CRO; }

<document,docstarttag,docendtag>{ero}[a-zA-Z][a-zA-Z0-9\-.]*[; ]  {
	char *c;
	char insertchar = 0;
	int end_blank = 0;

	templen = yyleng - 2; 
	sgml_yylval.string = CALLOC(char, templen+1);
	strncpy(sgml_yylval.string, &yytext[1],templen);
	sgml_yylval.string[templen] = '\0';
	for (c = sgml_yylval.string; *c; c++) *c = toupper(*c);
	
	c = sgml_yylval.string;

	if (yytext[yyleng-1] == ' ') {
	   yyless(yyleng-1);
	   parsed_file_chars -= 1;
	   end_blank = 1;
	}
#ifdef DEBUGGING_STUFF
	/* handle a couple cases where markup may have been translated */
	if (strcmp(sgml_yylval.string, "LT") == 0) {
	   insertchar = '<';
	}
	
	if (strcmp(sgml_yylval.string, "AMP") == 0) {
	   insertchar = '&';
	}
#endif
	if (insertchar) {
		/* take back the size of the matched reference */
       		parsed_file_chars -= yyleng; 
		temp_parse_buffer[parsed_file_chars++] = insertchar;
		/* set a flag showing the incoming buffer will be different */
		/* from the source buffer                                   */
		temp_parse_buffer_differs = 1;
		sgml_yylval.string[0] = insertchar;
		sgml_yylval.string[1] = '\0';
		return PCDATA_DATA;
	}


	sgml_yylval.hash_entry = Tcl_FindHashEntry(
			current_sgml_dtd->SGML_Entity_hash_tab,
			sgml_yylval.string);
	
	if (sgml_yylval.hash_entry != NULL)
		return ERO_REF;
	else { /* this must be an ampersand followed by a letter
		c = sgml_yylval.string; /* save old string ptr */
		sgml_yylval.string = strdup(yytext);
		if (c != NULL) FREE(c); 
		return PCDATA_DATA;	
	     }
	}

<document>[^<&\n]* { 
	sgml_yylval.string = strdup(yytext);
	return PCDATA_DATA;
	}

<charref>"RE" {
	char_insert_size += yyleng;
	char_insert = (char)13; 
	return RE_DCL;}

<charref>"RS" {
	char_insert_size += yyleng;
	char_insert = (char)10; 
	return RS_DCL;}

<charref>"SPACE" {
	char_insert_size += yyleng;
	char_insert = (char)32; 
	return SPACE_DCL;}

<charref>[a-zA-Z][a-zA-Z0-9\-.]* { 
	char_insert_size = 0;
	return SGMLNAME;}

<charref>[0-9]* { 
	char_insert_size += yyleng;
	sgml_yylval.intval = char_insert = (char) atoi(yytext);
	return NUMBER; }

<charref>{refc} {
	if (char_insert_size > 0) { /* this ignors names of functions */
		/* take back the size of the matched reference */
       		parsed_file_chars -= (yyleng + char_insert_size) ; 
		temp_parse_buffer[parsed_file_chars++] = char_insert;
		/* set a flag showing the incoming buffer will be different */
		/* from the source buffer                                   */
		temp_parse_buffer_differs = 1;
	}
	BEGIN(document); 
	return REFC;
     }


<charref>{space} {
	if (char_insert_size > 0) { /* this ignors names of functions */
		/* take back the size of the matched reference */
       		parsed_file_chars -= (yyleng + char_insert_size) ; 
		temp_parse_buffer[parsed_file_chars++] = char_insert;
		/* set a flag showing the incoming buffer will be different */
		/* from the source buffer                                   */
		temp_parse_buffer_differs = 1;
	}
	BEGIN(document); 
	return REFC;
     }

<sgml_decl,sgml_doctype,sgml_attlist,sgml_element,sgml_entity,groups,mdo_start,sgml_shortref,sgml_usemap,INITIAL,newinclude>{newline}  { 
	parsed_file_lineno++; /* this won't be accurate */}

.	{
	 
	 return yytext[0]; 
	}


<sgml_catalog>"PUBLIC" { return CATALOG_PUBLIC;}
 
<sgml_catalog>"ENTITY" { return CATALOG_ENTITY;}
 
<sgml_catalog>"DOCTYPE" { return CATALOG_DOCTYPE;}

<sgml_catalog>"LINKTYPE" { return CATALOG_LINKTYPE;}

<sgml_catalog>"NOTATION" { return CATALOG_NOTATION;}

<sgml_catalog>"OVERRIDE" { return CATALOG_OVERRIDE;}

<sgml_catalog>"SYSTEM" { return CATALOG_SYSTEM;}

<sgml_catalog>"SGMLDECL" { return CATALOG_SGMLDECL;}

<sgml_catalog>"DOCUMENT" { return CATALOG_DOCUMENT;}

<sgml_catalog>"CATALOG" { return CATALOG_CATALOG;}

<sgml_catalog>"BASE" { return CATALOG_BASE;}

<sgml_catalog>"DELEGATE" { return CATALOG_DELEGATE;}


<sgml_catalog>{param_lit}  {
	templen = yyleng - 2; 
	sgml_yylval.string = CALLOC(char, templen+1);
	strncpy(sgml_yylval.string, &yytext[1],templen);
	sgml_yylval.string[templen] = '\0';
	return CATALOG_NAME;
	}

<sgml_catalog>{sysid_token}  {
	sgml_yylval.string = strdup(yytext);
	return CATALOG_NAME;
	}

<sgml_catalog>{entity_token}  {
	sgml_yylval.string = strdup(yytext);
	return CATALOG_NAME;
	}



<sgml_catalog>"--"[^-]*"--" { } /* do nothing -- eats comments */
<sgml_catalog>{ws} { }
<sgml_catalog>{newline} { }

<sgml_catalog><<EOF>> {
			yyterminate();
			return CATALOG_EOF; }


<newinclude><<EOF>> { /* this is needed -- don't know why */
#ifdef DEBUGPARSER
   fprintf(stdout,"<<EOF>> #1 encountered sgml_state %d\n",
	sgml_state); 
#endif
		BEGIN(sgml_prev_state[--sgml_state]); 
        }

<<EOF>> { /* end of an input file */	

#ifdef DEBUGPARSER
   fprintf(stdout,"<<EOF>> encountered for include stack ptr %d\n",
	include_stack_ptr); 
#endif
	if (--include_stack_ptr < 0) {
		yyterminate();
		include_stack_ptr = 0;
		return(ENTITY_END);
	}
	else {
		/*fclose(YY_CURRENT_BUFFER->yy_input_file); */

		yy_delete_buffer(YY_CURRENT_BUFFER); 
		yy_switch_to_buffer(include_stack[include_stack_ptr]);
		yyin = include_stack[include_stack_ptr]->yy_input_file;
		BEGIN(sgml_prev_state[--sgml_state]); 
#ifdef DEBUGPARSER
   fprintf(stdout,"<<EOF>> begin sgml_state = %d\n", sgml_state); 
#endif

		if (string_entity) {
#ifdef DEBUGPARSER
   fprintf(stdout,"<<EOF>> is for string entity\n"); 
#endif
			return(END_INCLUDE);
}

	        if (include_entity) {
#ifdef DEBUGPARSER
   fprintf(stdout,"<<EOF>> is for include entity\n"); 
#endif
		        if (include_stack_ptr == 0) 
				include_entity = 0;
		        return(END_INCLUDE);
		}
		/* this seems to be required for system includes of  */
		/* things like DTD references -- the entity includes */
		/* don't need it --- probably a bug someplace        */
		sgml_push_back_token(sgml_yylval_include_back.tok_id); 
	        return(END_INCLUDE);
	}

	}

%%



int 
sgml_next_token()
{
  extern int document_tag_nesting;
  extern int force_document_end;
  int token;
  extern int sgml_parse_error;

  if (sgml_parse_error) {
	yyterminate();
	return(0);
  }
 
  /* check if there is a token pushed back or read ahead */
  if (sgml_yylval_pushback.tok_id != 0) {
	sgml_yylval = sgml_yylval_pushback;
        sgml_yylval_pushback.tok_id = 0;
	return (sgml_yylval.tok_id);
   }

   if (document_tag_nesting == 0 && force_document_end == 1) {
	yyterminate();
	return(0);
   }

   /* otherwise read the next token */
   token = yylex();

   sgml_yylval.tok_id = token;
   return (token);
} 

int sgml_push_back_token(int token)
{
    sgml_yylval_pushback = sgml_yylval;
    sgml_yylval_pushback.tok_id = token;
}


/* this routine is called by the parser created by LLgen */
int sgml_message(int token)
{
   extern int sgml_symb;
   extern SGML_DTD *current_sgml_dtd;

#ifdef DEBUGPARSER
   fprintf(stdout,"sgml_message called: %d\n", token); 
#endif

   switch (token) {
	case -1: 
#ifdef DEBUGPARSER
		fprintf(stdout,"Supposed to be end of file??\n"); 
#endif
		break;
	
	case 0: 
#ifdef DEBUGPARSER
		fprintf(stdout,
		  "token deleted? at line %d: sgml_symb = %d value %s  EXITING ...\n",
		  parsed_file_lineno, sgml_symb, sgml_yylval.string); 
		
                exit(1);
#endif
		break;
	default: 
#ifdef DEBUGPARSER
		fprintf(stdout,"token pushed back at line %d: sgml_symb = %d value %s \n",
			parsed_file_lineno, sgml_symb, sgml_yylval.string);  
#endif                
		sgml_push_back_token(token);
		break;
   }

}


char *build_full_name( char *in_name)
{
  char *path_name = NULL; 
  char *getcwd();
  char *full_name = NULL;
  int path_len = 0;
  int name_len = 0;

        name_len = strlen(in_name);

        if (in_name[0] == '/') /* assume it is already a path name */
		return (strdup(in_name));

	/* otherwise assume that the name only is provided */
        path_name = getcwd (NULL, 500);
        path_len = strlen(path_name);

        full_name = MALLOC(path_len + name_len + 10 /*extension slush*/);
        sprintf(full_name,"%s/%s",path_name,in_name);

	return(full_name);
}


int include_buffer_switch (char *new_file_name)
{
  char *path_name = NULL;
  extern int sgml_symb;

	if (include_stack_ptr >= MAX_INCLUDE_DEPTH) {
		fprintf(LOGFILE,"Too deeply nested on SYSTEM include\n"); 
		exit(1);
	}
#ifdef DEBUGPARSER
   fprintf(stdout,"adding include stack entry: %d\n", include_stack_ptr); 
#endif
	include_stack[include_stack_ptr++] = YY_CURRENT_BUFFER;

   	/* assume that the last token was the name */

        path_name = build_full_name(new_file_name);

	yyin = fopen(path_name,"r");

        if (!yyin) { /* try adding default extension .dtd */
		strcat(path_name, ".dtd");
		yyin = fopen(path_name,"r");
	}

	if (!yyin) {
		fprintf(LOGFILE,
		      "SGML_SCAN: Could not open SYSTEM include '%s'\n",
			path_name); 
		perror(NULL);

		exit(1);
	}
	
	yy_switch_to_buffer(yy_create_buffer(yyin,YY_BUF_SIZE));

	sgml_prev_state[sgml_state++] = YY_START;
#ifdef DEBUGPARSER
   fprintf(stdout,"include buffer switch: sgml_state = %d\n", sgml_state); 
#endif

	BEGIN(newinclude);
	
        sgml_yylval_include_back.tok_id = sgml_symb;

	FREE(path_name);
}


int string_buffer_switch (char *new_contents, int nested)
{
	YY_BUFFER_STATE string_buffer;
	int new_content_len;

#ifdef DEBUGPARSER
   fprintf(stdout,"Switching to string buffer for string entity\n"); 
#endif

	if (new_contents == NULL) {
		fprintf(LOGFILE, "Null contents in string buffer switch\n");
		exit(1);
	}

	new_content_len = strlen(new_contents);

	if (include_stack_ptr >= MAX_INCLUDE_DEPTH) {
		fprintf(LOGFILE,"Too deeply nested on SYSTEM include\n"); 
		exit(1);
	}
	
	if (nested)
		include_stack[include_stack_ptr++] = YY_CURRENT_BUFFER;

#ifdef DEBUGPARSER
   fprintf(stdout,"include stack ptr changed to %d\n", include_stack_ptr); 
#endif

	if (!yyin) {
		yyin = tmpfile();
		if (!yyin) {
			perror("Couldn't open tmpfile in sgml_scan");
			exit(1);
	        }
	}

	string_buffer = yy_create_buffer(yyin,new_content_len+1);

	/* copy data to input buffer */
	strcpy(string_buffer->yy_ch_buf, new_contents);
	string_buffer->yy_ch_buf[new_content_len] = '\0';
	string_buffer->yy_ch_buf[new_content_len+1] = '\0';

	/* set current position in input buffer to beginning */
	string_buffer->yy_buf_pos = string_buffer->yy_ch_buf;

	/* Size of input buffer in bytes, not including room for EOB
	 * characters.
	 */
 	/* string_buffer->yy_buf_size; should be OK already */

	/* Number of characters read into yy_ch_buf, not including EOB
	 * characters.
	 */
	string_buffer->yy_n_chars = new_content_len;

	/* Whether to try to fill the input buffer when we reach the
	 * end of it.
	 */
	string_buffer->yy_fill_buffer = 0;  

	/* Whether we've seen an EOF on this buffer. */
	/* string_buffer->yy_eof_status = 1; */

	/* #define EOF_PENDING 1 */
	/* #define EOF_NOT_SEEN 0 */
	/* "Pending" happens when the EOF has been seen but there's still
	 * some text to process.  Note that when we actually see the EOF,
	 * we switch the status back to "not seen" (via sgml_restart()), so
	 * that the user can continue scanning by just pointing yyin at
	 * a new input file.
	 */

	/* switch scanning to the new buffer */
	yy_switch_to_buffer(string_buffer);
	
	string_entity = 1;
	/* save state, but no need to change it     */
	/* this is so it can be "restored" at "EOF" */
	sgml_prev_state[sgml_state++] = YY_START;
#ifdef DEBUGPARSER
   fprintf(stdout,"String entity switch sgml_state = %d\n", sgml_state); 
#endif


}


start_document_parsing () 
{
  extern int document_tag_nesting;
  extern int force_document_end;
  extern int sgml_parse_error;

	parsed_file_lineno = 1;
	parsed_file_chars = 0;
	document_tag_nesting = 0;
	force_document_end = 0;
	sgml_parse_error = 0;
	sgml_state = 0;
	include_stack_ptr = 0;

	BEGIN(document);

}

start_dtd_parsing () 
{
  extern int document_tag_nesting;
  extern int force_document_end;
  extern int sgml_parse_error;

	parsed_file_lineno = 1;
	parsed_file_chars = 0;
	document_tag_nesting = 0;
	force_document_end = 0;
	sgml_parse_error = 0;
	sgml_state = 0;
	include_stack_ptr = 0;

	BEGIN(INITIAL);

}

start_catalog_parsing () 
{
  extern int document_tag_nesting;
  extern int force_document_end;
  extern int sgml_parse_error;
	parsed_file_lineno = 1;
	parsed_file_chars = 0;
	document_tag_nesting = 0;
	force_document_end = 0;
	sgml_parse_error = 0;
	sgml_state = 0;
	include_stack_ptr = 0;

	BEGIN(sgml_catalog);

}

/* switch input to the entity referenced */
switch_input_to_entity (SGML_Entity *ent)
{
  

  if (ent->entity_text_type == EXTERNAL_ENTITY_SPEC ) {
	if (ent->system_name != NULL) {
		include_buffer_switch (ent->system_name);
#ifdef DEBUGPARSER
   fprintf(stdout,"Switched input to system file: %s\n", ent->system_name); 
#endif

	}
	else if (ent->public_name != NULL) {
		/* include_buffer_switch (ent->public_name); */
		fprintf(LOGFILE, "Public Entity Name with no matching catalog entry: \n'%s'\n", ent->public_name);
#ifdef DEBUGPARSER
	fprintf(stdout, "Public Entity Name with no matching catalog entry: \n'%s'\n", ent->public_name);
#endif

	}
  }
  else if (ent->entity_text_type == PARAMETER_LIT_TEXT ) {
	string_buffer_switch(ent->entity_text_string,1 /* this is nested */);
  }
  include_entity = 1;
}

int yywrap () 
{
  return (1); 
}

int scan_backup() {
	BEGIN(document);
}

int clean_up_scan() {
  if (YY_CURRENT_BUFFER) {
     yy_delete_buffer(YY_CURRENT_BUFFER);
     YY_CURRENT_BUFFER = NULL;
     yy_init = 1;
  }
  
}
